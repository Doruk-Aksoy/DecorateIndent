actor KnightCrushBoss : BaseMM8BDMWep_CBM
{
tag "$TAGC_6D"
dropitem "KnightCrushWepCDropped"
Weapon.AmmoUse 0
Weapon.AmmoGive 28
Obituary "$OB_KNIGHTCRUSH"
weapon.ammotype "KnightmanAmmo"
States
{
Spawn:
C_06 D 1
loop
Ready:
KNIA A 0 ACS_ExecuteAlways(998,0,DYE_KNIGHTMAN)
KNIA A 0 A_TakeInventory("StopShield",9)
KNIA A 0 A_GunFlash
Goto Ready1
Ready1:
KNIA A 0 A_JumpIfInventory("KnightmanAmmo",28,"Ready2")
KNIA A 3 A_WeaponReady(8)
KNIA A 0 A_GiveInventory("KnightmanAmmo",1)
loop
Ready2:
KNIA A 3 A_WeaponReady
KNIA A 0 A_GiveInventory("KnightmanAmmo",1)
loop
Deselect:
KNIA A 0
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
KNIA A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
KNIA A 1 A_Raise
Loop
Fire:
KNIA B 0 A_PlaySoundEx("weapon/KnightShot","Weapon")
KNIA B 0 A_GiveInventory("StopShield",1)
KNIA B 3 A_GiveInventory("KnightmanFlag",1)
KNIA C 0 A_TakeInventory("KnightDouble",1)
KNIA C 3 A_GiveInventory("KnightmanMain_CI",1)
KNIA D 6
Goto Check

Check:
KNIA D 0 A_JumpIfInventory("KnightDouble",1,"Return")
KNIA D 1 A_GiveInventory("KnightFlag",1)
KNIA D 0 A_JumpIfInventory("KnightFlag",35,"Return")
loop

Return:
KNIA D 2 A_TakeInventory("KnightmanFlag",999)
KNIA C 2
KNIA B 4 A_TakeInventory("KnightFlag",999)
Goto Ready+1

AltFire:
KNIA A 0 A_JumpIfInventory("KnightmanAmmo",28,"ShieldCharge")
goto Ready+1
ShieldCharge:
KNIA G 0 A_GiveInventory("VivifyDelay7",1)
KNIA G 0 A_Refire(1)
KNIA G 0 A_TakeInventory("KnightmanAmmo",99)
KNIA G 2 SetPlayerProperty(0,1,0)
KNIA G 0 A_PlaySoundEx("weapon/chargekick","Weapon")
KNIA G 0 A_GiveInventory("StopShield",1)
KNIA G 0 A_GiveInventory("InvulnerableOn_Pickup",1)
KNIA H 1 A_ChangeVelocity(Cos(pitch)*6,0,-sin(pitch)*3,1)//A_Recoil(-6)
KNIA H 0 A_FireCustomMissile("KnightRush")
KNIA H 0 A_GiveInventory("Repulsion",1)
KNIA H 1 A_ChangeVelocity(Cos(pitch)*6,0,-sin(pitch)*3,1)
KNIA H 0 A_FireCustomMissile("KnightRush")
KNIA H 0 A_GiveInventory("Repulsion",1)
KNIA H 1 A_ChangeVelocity(Cos(pitch)*6,0,-sin(pitch)*3,1)
KNIA H 0 A_FireCustomMissile("KnightRush")
KNIA H 0 A_GiveInventory("Repulsion",1)
KNIA H 1 A_ChangeVelocity(Cos(pitch)*6,0,-sin(pitch)*3,1)
KNIA H 0 A_FireCustomMissile("KnightRush")
KNIA H 0 A_GiveInventory("Repulsion",1)
KNIA G 1 SetPlayerProperty(0,0,0)
KNIA G 0 A_GiveInventory("InvulnerableOff_Pickup",1)
KNIA G 0 A_TakeInventory("StopShield",9)
KNIA G 3 A_GunFlash
Goto Ready1

Flash:
TNT1 A 0 A_SpawnItemEx("KnightShieldX",28,17,8,momx,momy,momz,0,8,0)
TNT1 A 1 A_JumpIfInventory("StopShield",1,"NoFlash")
TNT1 A 0 A_JumpIfInventory("IsDead",1,"NoFlash")
loop

}
}

actor KnightmanAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}


actor KnightmanFlag : OnceC{}

actor KnightmanCrusher
{
Translation "198:198=75:75"
PROJECTILE
damagetype "KnightCrush"
Obituary "$OB_KNIGHTCRUSH"
+RIPPER
+DONTSPLASH
+HEXENBOUNCE
+SKYEXPLODE
+THRUGHOST
BounceFactor 1.0
WallBounceFactor 1.0
damage (0)
Radius 8
Height 8
speed 40
scale 2.5
States
{
Spawn:
KNIA I 0
KNIA II 1 A_SpawnItemEx("KnightmanCrusherDamager",0,0,0,momx,momy,momz,0,8,0)
KNIA I 0 A_ChangeFlag("THRUGHOST",0)
KNIA IIIIIIII 1 A_SpawnItemEx("KnightmanCrusherDamager",0,0,0,momx,momy,momz,0,8,0)
KNIA I 0 A_TakeFromTarget("KnightFlag",999)
KNIA IIIIIIII 1 A_SpawnItemEx("KnightmanCrusherDamager",0,0,0,momx,momy,momz,0,8,0)
KNIA I 0 A_SpawnItemEx("KnightmanCrusherReturn",0,0,0,0,0,0,0,1)
stop
Death:
KNIA I 0 A_GiveToTarget("KnightDouble",1)
KNIA I 0 A_TakeFromTarget("KnightFlag",999)
stop
}
}

actor KnightmanCrusherB : KnightmanCrusher{translation "198:198=74:74"}
actor KnightmanCrusherR : KnightmanCrusher{translation "198:198=41:41"}
actor KnightmanCrusherO : KnightmanCrusher{translation "198:198=128:128"}
actor KnightmanCrusherP : KnightmanCrusher{translation "198:198=232:232"}

actor KnightReturnerSound : BasicHelper
{
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_PlaySoundEx("weapon/KnightReturn","Weapon")
stop
}
}

actor KnightmanCrusherReturn : KnightmanCrusher
{
+NOINTERACTION
+SEEKERMISSILE
ReactionTime 87//about 10 seconds
States
{
Spawn:
KNIA I 0
KNIA I 0 A_RearrangePointers(AAPTR_DEFAULT,AAPTR_DEFAULT,AAPTR_TARGET)//Target >> Tracer
KNIA I 0 A_FaceTarget
goto Spawn2
Spawn2:
KNIA I 0 A_TakeFromTarget("KnightFlag",99)
KNIA I 0 A_JumpIfCloser(46,"Death")
KNIA I 0 A_SpawnItemEx("KnightReturnerSound")
KNIA II 0 A_SeekerMissile(90,90,SMF_PRECISE)
KNIA I 1 A_SpawnItemEx("KnightmanCrusherDamager",0,0,0,momx,momy,momz,0,8)
KNIA I 0 A_JumpIfCloser(46,"Death")
KNIA I 1 A_SpawnItemEx("KnightmanCrusherDamager",0,0,0,momx,momy,momz,0,8)
KNIA I 0 A_JumpIfCloser(46,"Death")
KNIA I 1 A_SpawnItemEx("KnightmanCrusherDamager",0,0,0,momx,momy,momz,0,8)
KNIA I 0 A_JumpIfCloser(46,"Death")
KNIA I 1 A_SpawnItemEx("KnightmanCrusherDamager",0,0,0,momx,momy,momz,0,8)
KNIA I 0 A_JumpIfCloser(46,"Death")

KNIA I 0 A_CountDown
KNIA I 0 A_JumpIf(ACS_ExecuteWithResult(C_CHECK_TARGET_OOC)==0,"Death")
loop
}
}

actor KnightmanCrusherDamager
{
PROJECTILE
damagetype "KnightCrush"
Obituary "$OB_KNIGHTCRUSH"
+RIPPER
+DONTBLAST
damage (8)//11
radius 12
height 12
States
{
Spawn:
TNT1 A 2
stop
}
}

actor KnightShieldX : BasicHelper
{
+NOTIMEFREEZE
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox",0,-24,0,velx,vely,velz,0,8)
TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox",0,-16,0,velx,vely,velz,0,8)
TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox",0,-8,0,velx,vely,velz,0,8)
TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox",0,0,0,velx,vely,velz,0,8)
TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox",0,8,0,velx,vely,velz,0,8)
TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox",-8,8,0,velx,vely,velz,0,8)
TNT1 A 0 A_SpawnItemEx("KnightShieldHitbox",-16,8,0,velx,vely,velz,0,8)
TNT1 A 1
stop
}
}

actor NewProtoShieldHitbox {
	+MISSILE
	Speed 0
	+DONTSPLASH
	Damage 0
	Height 42
	Radius 5
	health 9999
	scale 2.5
	damagefactor "ProtoBuster", 1.0
	+SHOOTABLE
	+NOBLOOD
	-SOLID
	+NOTIMEFREEZE
	+NOGRAVITY
	+HEXENBOUNCE
	+BOUNCEONACTORS
	painchance 256
	bloodtype ""
	+DONTRIP
	+GHOST
	+NORADIUSDMG
	+DONTBLAST
	+DONTDRAIN
	States {
		Spawn:
			TNT1 A 0
			ARRS F 2
		stop
		Death:
			TNT1 A 0 //A_PlaySound("item/protoreflect")
			ARRS F 1
		stop
		Pain:
			TNT1 A 0 
			TNT1 A 0 A_SpawnItemEx("ProtoShieldDeahthFX")
			TNT1 A 1 A_PlaySound("item/protoreflect")
		stop
	}
}

actor KnightShieldHitbox : NewProtoShieldHitbox {
	states {
		Pain:
			TNT1 A 0 
			TNT1 A 0 A_SpawnItemEx("KnightShieldDeathFX")
			TNT1 A 2 A_PlaySound("item/protoreflect")
		stop
	}
}

actor KnightShieldDeathFX : BasicClientSide
{
alpha 0.9
renderstyle add
states
{
Spawn:
KNIA L 0 A_SetScale(scaleX + 0.1, 2.5) 
KNIA L 1 A_FadeOut(0.05)
loop
}
}

actor KnightRush : BasicExplosion
{
Damagetype "KnightRush"
Obituary "$OB_KNIGHTRUSH"
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_Explode(5,120,0,0,16)
stop
}
}


actor KnightmanMain_CI : TeamColor_CI
{
states
{
FireX:
TNT1 A 0 A_FireCustomMissile("KnightmanCrusher",0,-8,0,0)
goto Done
FireB:
TNT1 A 0 A_FireCustomMissile("KnightmanCrusherB",0,-8,0,0)
goto Done
FireR:
TNT1 A 0 A_FireCustomMissile("KnightmanCrusherR",0,-8,0,0)
goto Done
FireO:
TNT1 A 0 A_FireCustomMissile("KnightmanCrusherO",0,-8,0,0)
goto Done
FireP:
TNT1 A 0 A_FireCustomMissile("KnightmanCrusherP",0,-8,0,0)
goto Done
}
}


